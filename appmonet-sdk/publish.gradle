apply plugin: 'com.novoda.bintray-release'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def url = 'https://github.com/AppMonet/android-sdk'
def bintrayRepository = project.hasProperty('env') ? "MonetBidder" : "monet_android_dev"

def apiKey = properties.getProperty("bintray.apiKey", "")
def user = properties.getProperty("bintray.user", "")

tasks.withType(Javadoc).all {
  enabled = false
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  def task = project.tasks.create "jar${name.capitalize()}", Jar

  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  artifacts.add('archives', task)
}

android.libraryVariants.all { variant ->
  task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
    title = project.ext.projectTitle
    description "Generates Javadoc for $variant.name."
    source = variant.javaCompile.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile

    options {
      links "https://docs.oracle.com/javase/8/docs/api/"
      linksOffline "https://developer.android.com/reference",
          "${android.sdkDirectory}/docs/reference"
    }

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
  }
}

publish {
  repoName = bintrayRepository
  userOrg = "appmonet"
  groupId = project.group
  artifactId = project.ext.projectName
  uploadName = "monetbidder"
  publishVersion = project.version
  desc = project.description
  website = url
  repository = url
  dryRun = false
  bintrayUser = user
  bintrayKey = apiKey
}
