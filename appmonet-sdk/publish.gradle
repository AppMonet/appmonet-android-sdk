apply plugin: 'maven-publish'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/AppMonet/appmonet-android-sdk")
            credentials {
                username = System.getenv('GITHUB_USER') ?: properties['GITHUB_USER']
                password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: properties['GITHUB_PERSONAL_ACCESS_TOKEN']
            }
        }
    }
    publications {
        "${project.ext.build}Release"(MavenPublication) {
            groupId project.group
            artifactId project.ext.projectName
            version project.version
            artifact("$buildDir/outputs/aar/${project.ext.aarName}-release.aar")
            artifact(sourceJar)
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}