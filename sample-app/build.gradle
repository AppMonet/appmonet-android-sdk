apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
  signingConfigs {
    appmonet {
      keyAlias getProp("signing.keyAlias")
      keyPassword getProp("signing.keyPassword")
      storeFile file(getProp("signing.storeFile", "appmonet"))
      storePassword getProp("signing.storePassword")
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  compileSdkVersion 28

  defaultConfig {
    applicationId "com.monet.app"
    minSdkVersion 19
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    buildConfigField "String", "INTERSTITIAL_AD_UNIT_ID",
        "\"" + getProp("INTERSTITIAL_AD_UNIT_ID") + "\""
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.appmonet
    }
    debug {
      minifyEnabled false
    }
  }

  flavorDimensions "default"
  productFlavors {
    dfp {
      applicationIdSuffix ".dfp"
      versionNameSuffix "-dfp"
      resValue "string", "BANNER_AD_UNIT_ID", getProp("BANNER_AD_UNIT_ID")
    }
    admob {
      applicationIdSuffix ".admob"
      versionNameSuffix "-admob"
      matchingFallbacks = ['dfp']
    }
    mopub {
      applicationIdSuffix ".mopub"
      versionNameSuffix "-mopub"
    }
    if (standaloneEnabled()) {
      appmonet {
        applicationIdSuffix ".appmonet"
        versionNameSuffix "-appmonet"
      }
    }
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

dependencies {
  implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
  implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
  mopubImplementation('com.mopub:mopub-sdk:5.13.0@aar') {
    transitive = true
  }
  mopubImplementation project(':appmonet-sdk::appmonet-sdk-mopub')

  dfpImplementation "com.google.android.gms:play-services-ads:19.3.0"
  dfpImplementation project(':appmonet-sdk::appmonet-sdk-dfp')

  admobImplementation "com.google.android.gms:play-services-ads:19.3.0"
  admobImplementation project(':appmonet-sdk::appmonet-sdk-dfp')

  if (standaloneEnabled()) {
    if (file('../appmonet-sdk/appmonet-sdk-bidder/src').exists()) {
      appmonetImplementation project(':appmonet-sdk::appmonet-sdk-bidder')
    } else {
      appmonetImplementation('com.monet:appmonet-bidder:4.0.4@aar')
    }
  }

  implementation fileTree(dir: "libs", include: ["*.jar"])
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'androidx.core:core-ktx:1.3.1'
  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
  implementation 'com.google.android.material:material:1.2.0'

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

def getProp(String property, String defaultProperty = "") {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty(property, defaultProperty)
}
